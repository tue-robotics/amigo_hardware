########################################################################
#                                                                      #
# Base controller deployer file 									   #
#                                                                      #
# Tim Clephas                                                          #
# Mostly 2011                                                          #
#                                                                      #
########################################################################

#### IMPORT PACKAGE ####
# To be able to load components from this package and all packages in the manifest
import("amigo_base_controller")
trigger
Supervisor.NameBodyPart( 1, "base")

#### DECLARATION OF PARAMETERS ####
var double Ts = 0.001
var double WHEELRAD = 0.075*0.9825; # Corrected for effective wheel radius
var double DIS2CENT = 0.29072
var double HALFSQRT2 = 0.7071
var double TORQUE2CURRENT = 1.0/0.0302; # [Amp/Nm]
var double CURRENT2VOLT = 1.0/1.0
var double GEARRATIO = 8.0/343.0
var double TWOPI = 2*3.141592
var double ENCODERCOUNTS = 500.0*4.0
var double ENC2SI = TWOPI*GEARRATIO/ENCODERCOUNTS

### READ WHEEL ENCODERS ###
loadComponent("BASE_ReadEncoders","SOEM::ReadEncoders")
addPeer("Supervisor","BASE_ReadEncoders")
setActivity("BASE_ReadEncoders",Ts,HighestPriority,ORO_SCHED_RT)
BASE_ReadEncoders.encoderbits = 65536
BASE_ReadEncoders.enc2SI = array (ENC2SI, ENC2SI, ENC2SI, ENC2SI)
BASE_ReadEncoders.configure

### DECOUPLE POSITION ###
loadComponent("BaseDecouplePosition","MatrixTransform")
addPeer("Supervisor","BaseDecouplePosition")
setActivity("BaseDecouplePosition",0.0,HighestPriority,ORO_SCHED_RT)
BaseDecouplePosition.Nrows = 3
BaseDecouplePosition.Ncolumns = 4
BaseDecouplePosition.configure
BaseDecouplePosition.function1 = array (-0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2)
BaseDecouplePosition.function2 = array ( 0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2)
BaseDecouplePosition.function3 = array ( 0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT)

#### LOAD COMPONENT TO PUBLISH ODOMETRY ###
loadComponent("Odometry","MSG::PublishOdometry")
addPeer("Supervisor","Odometry")
Odometry.base_link_frame = "/amigo/base_link"
Odometry.odom_frame = "/amigo/odom"
Odometry.configure
setActivity("Odometry",0.05,LowestPriority,ORO_SCHED_OTHER)

### READ REFERENCE VELOCITIES ###
loadComponent("BaseReadReferenceVelocities","MSG::ReadTwistMsg")
addPeer("Supervisor","BaseReadReferenceVelocities")
#loadService("BaseReadReferenceVelocities","rosparam")
#BaseReadReferenceVelocities.rosparam.refreshProperties()
BaseReadReferenceVelocities.max_start_vel = 0.7
BaseReadReferenceVelocities.max_acc = array (0.7, 0.7, 2.0)
BaseReadReferenceVelocities.max_interval = 1.0
BaseReadReferenceVelocities.configure
setActivity("BaseReadReferenceVelocities",0.01,LowestPriority,ORO_SCHED_OTHER)

### DECOUPLE REFERENCE ###
loadComponent("BaseDecoupleVelReference","MatrixTransform")
addPeer("Supervisor","BaseDecoupleVelReference")
BaseDecoupleVelReference.Nrows = 4
BaseDecoupleVelReference.Ncolumns = 3
BaseDecoupleVelReference.configure
setActivity("BaseDecoupleVelReference",0.0,LowestPriority,ORO_SCHED_OTHER)
BaseDecoupleVelReference.function1 = array ( -HALFSQRT2/WHEELRAD,  HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function2 = array ( -HALFSQRT2/WHEELRAD, -HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function3 = array (  HALFSQRT2/WHEELRAD, -HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function4 = array (  HALFSQRT2/WHEELRAD,  HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )


### VELOCITY TO POSITION ###
loadComponent("BaseWheelVel2WheelPos","MATH::Integrator")
addPeer("Supervisor","BaseWheelVel2WheelPos")
BaseWheelVel2WheelPos.vector_size = 4
BaseWheelVel2WheelPos.configure
setActivity("BaseWheelVel2WheelPos",0.0,LowestPriority,ORO_SCHED_OTHER)


### CALCULATE ERRORS ###
loadComponent("BASE_ComputeErrors","MATH::Subtraction")
addPeer("Supervisor","BASE_ComputeErrors")
BASE_ComputeErrors.vectorsize = 4;
BASE_ComputeErrors.configure
setActivity("BASE_ComputeErrors",0.0,HighestPriority,ORO_SCHED_RT)


### GAIN ###
loadComponent("BaseControllerGains","MATH::Gains")
addPeer("Supervisor","BaseControllerGains")
BaseControllerGains.vectorsize = 4;
#BaseControllerGains.gain = array (0.4, 0.4, 0.4, 0.4) # 10-08-12
BaseControllerGains.gain = array (5.0, 5.0, 5.0, 5.0)
BaseControllerGains.configure
setActivity("BaseControllerGains",0.0,HighestPriority,ORO_SCHED_RT)


### INTEGRATOR ###
loadComponent("BaseIntegrator","FILTERS::WeakIntegrator")
addPeer("Supervisor","BaseIntegrator")
BaseIntegrator.vector_size = 4;
BaseIntegrator.sampling_time = Ts
BaseIntegrator.zero_frequency = array (0.0001, 0.0001, 0.0001, 0.0001)
BaseIntegrator.configure
setActivity("BaseIntegrator",0.0,HighestPriority,ORO_SCHED_RT)


### LEADLAG ###
loadComponent("BaseLeadLag","FILTERS::LeadLags")
addPeer("Supervisor","BaseLeadLag")
BaseLeadLag.vector_size = 4;
BaseLeadLag.sampling_time = Ts
BaseLeadLag.zero_frequency = array (15.0, 15.0, 15.0, 15.0)
BaseLeadLag.pole_frequency = array (50.0, 50.0, 50.0, 50.0)
BaseLeadLag.configure
setActivity("BaseLeadLag",0.0,HighestPriority,ORO_SCHED_RT)


### LOWPASS ###
loadComponent("BaseSecondOrderLowPass","FILTERS::SecondOrderLowPasses")
addPeer("Supervisor","BaseSecondOrderLowPass")
BaseSecondOrderLowPass.vector_size = 4;
BaseSecondOrderLowPass.sampling_time = Ts
BaseSecondOrderLowPass.pole_frequency = array (100.0, 100.0, 100.0, 100.0)
BaseSecondOrderLowPass.pole_damping = array (0.7, 0.7, 0.7, 0.7)
BaseSecondOrderLowPass.configure
setActivity("BaseSecondOrderLowPass",0.0,HighestPriority,ORO_SCHED_RT)


### SI2VOLT ###
loadComponent("BaseSI2Volt","MATH::Gains")
addPeer("Supervisor","BaseSI2Volt")
BaseSI2Volt.vectorsize = 4;
var double SI2V = TORQUE2CURRENT * CURRENT2VOLT * GEARRATIO
BaseSI2Volt.gain = array (SI2V, SI2V, SI2V, SI2V)
BaseSI2Volt.configure
setActivity("BaseSI2Volt",0.0,HighestPriority,ORO_SCHED_RT)




### Now the whole control loop is closed. Lets add some aditional components for fun and safety:


#### LOAD COMPONENT FOR SAFETY ###
loadComponent("BaseSafety","AMIGO::BaseSafety")
addPeer("Supervisor","BaseSafety")
BaseSafety.max_velocities = array (1.8, 1.0, 3.1415)
BaseSafety.max_errors = array ( 2.2, 2.2, 2.2, 2.2 )
BaseSafety.max_voltage = 9.9
BaseSafety.configure
setActivity("BaseSafety",0.0,HighestPriority/2,ORO_SCHED_RT)


#### LOAD COMPONENT TO PUBLISH DIAGNOSTICS ###
loadComponent("BaseDiagnostics","ROS::RosDiagnostics")
addPeer("Supervisor","BaseDiagnostics")
BaseDiagnostics.statusname = "BaseController"
BaseDiagnostics.NumberOfVectorPorts = 3
BaseDiagnostics.NumberOfBoolPorts = 1
BaseDiagnostics.configure
setActivity("BaseDiagnostics",1.0,LowestPriority,ORO_SCHED_OTHER)
BaseDiagnostics.vecname1 = "Reference"
BaseDiagnostics.vecname2 = "Error"
BaseDiagnostics.vecname3 = "Voltage"
BaseDiagnostics.boolname1 = "Amplifiers"

#### LOAD COMPONENT TO PUBLISH STATUS ###
loadComponent("BaseStatus","ROS::BoolToROS")
addPeer("Supervisor","BaseStatus")
BaseStatus.NumberOfBoolPorts = 1
BaseStatus.configure
setActivity("BaseStatus",0.5,LowestPriority,ORO_SCHED_OTHER)

#### SET UP JOINTSTATE AGGREGATOR ####
# To Do JointState message base

#### CONNECT COMPONENTS ####
connect ("Soem.Slave_1002.Slave_1002_value","BASE_ReadEncoders.enc1_in", ConnPolicy() ); #enc1[-]
connect ("Soem.Slave_1003.Slave_1003_value","BASE_ReadEncoders.enc2_in", ConnPolicy() ); #enc2[-]
connect ("Soem.Slave_1004.Slave_1004_value","BASE_ReadEncoders.enc3_in", ConnPolicy() ); #enc3[-]
connect ("Soem.Slave_1005.Slave_1005_value","BASE_ReadEncoders.enc4_in", ConnPolicy() ); #enc4[-]
connect ("BASE_ReadEncoders.out","BaseDecouplePosition.in", ConnPolicy() )
connect ("BaseDecouplePosition.out", "Odometry.pos", ConnPolicy() );
connect ("BaseWheelVel2WheelPos.out","BASE_ComputeErrors.in_plus", ConnPolicy() );
connect ("BASE_ReadEncoders.out","BASE_ComputeErrors.in_minus", ConnPolicy() ); #[m \n m \n rad]
connect ("BASE_ComputeErrors.out","BaseControllerGains.in", ConnPolicy() )
connect ("BaseControllerGains.out","BaseIntegrator.in", ConnPolicy() )
connect ("BaseIntegrator.out","BaseLeadLag.in", ConnPolicy() )
connect ("BaseLeadLag.out","BaseSecondOrderLowPass.in", ConnPolicy() )
connect ("BaseSecondOrderLowPass.out","BaseSI2Volt.in", ConnPolicy() )
connect ("BaseSI2Volt.out", "AnalogOuts.wheels", ConnPolicy() ); # Send to Soem again for output
connect ("BaseReadReferenceVelocities.out", "BaseSafety.ref", ConnPolicy() )
connect ("BaseReadReferenceVelocities.out","BaseDecoupleVelReference.in", ConnPolicy() )
connect ("BaseDecoupleVelReference.out","BaseWheelVel2WheelPos.in", ConnPolicy() )
connect ("BASE_ReadEncoders.out","BaseWheelVel2WheelPos.initial", ConnPolicy() ); # Use current position as initial position
connect ("BASE_ComputeErrors.out", "BaseSafety.error", ConnPolicy() )
connect ("BaseSI2Volt.out", "BaseSafety.voltage", ConnPolicy() )
#connect ("BaseSafety.wheel_amplifiers", "DigitalOuts.in1", ConnPolicy() ); # for reading back the ebutton info
connect ("BaseSafety.wheel_amplifiers", "DigitalOuts2.in2", ConnPolicy() ); # amplifiers
connect ("BaseReadReferenceVelocities.out", "BaseDiagnostics.vec1", ConnPolicy() )
connect ("BASE_ComputeErrors.out", "BaseDiagnostics.vec2", ConnPolicy() )
connect ("BaseSI2Volt.out", "BaseDiagnostics.vec3", ConnPolicy() )
connect ("BaseSafety.wheel_amplifiers", "BaseDiagnostics.bool1", ConnPolicy() )
connect ("BaseSafety.wheel_amplifiers", "BaseStatus.bool_in1", ConnPolicy() )

#### CONNECT ROS STREAMS ####
stream( "Supervisor.base_fireup", ros.topic("/base_fireup") )
stream( "Supervisor.base_enabled", ros.topic("/base_enabled"));

stream("Odometry.odom", ros.topic("/amigo/base/measurements"));
stream("Odometry.reset", ros.topic("/amigo/base/reset_odometry"));
stream ("BaseReadReferenceVelocities.cmd_vel", ros.topic("/amigo/base/references"));
stream("BaseDiagnostics.diagnostics", ros.topic("/diagnostics"));
stream("BaseDiagnostics.diagnostics", ros.topic("/base_diagnostics"));
stream("BaseStatus.bool_out1", ros.topic("/base_status"));


#### START COMPONENTS ####
Supervisor.AddAllwaysOnPeer ("BASE_ReadEncoders")
Supervisor.AddAllwaysOnPeer ("BaseDecouplePosition")
Supervisor.AddAllwaysOnPeer ("Odometry")

Supervisor.AddPeerToBodyPart ("BaseReadReferenceVelocities",1 )
Supervisor.AddPeerToBodyPart ("BaseDecoupleVelReference",1 )
Supervisor.AddPeerToBodyPart ("BaseWheelVel2WheelPos",1 )
Supervisor.AddPeerToBodyPart ("BASE_ComputeErrors",1 )
Supervisor.AddPeerToBodyPart ("BaseControllerGains",1 )
Supervisor.AddPeerToBodyPart ("BaseLeadLag",1 )
Supervisor.AddPeerToBodyPart ("BaseIntegrator",1 )
Supervisor.AddPeerToBodyPart ("BaseSecondOrderLowPass",1 )
Supervisor.AddPeerToBodyPart ("BaseSI2Volt",1 )

Supervisor.AddAllwaysOnPeer ("BaseSafety" )
Supervisor.AddAllwaysOnPeer ("BaseDiagnostics")
Supervisor.AddAllwaysOnPeer ("BaseStatus")
