########################################################################
#                                                                      #
# Base controller deployer file to load the controllers for the wheels #
#                                                                      #
# Tim Clephas                                                          #
# Mostly 2011                                                          #
#                                                                      #
# Uses the DeployerComponent available within the OCL for deploying    #
# and connecting components.                                           #
#                                                                      #
########################################################################

### Import amigo_base_controller package. All the components in this package,       ###
### as well as all components from packages mentioned in the manifest can be loaded ###
import("amigo_base_controller")

### Declare several variables that are typical for this setup ###
var double Ts = 0.001
var double WHEELRAD = 0.075*0.9825; # Corrected for effective wheel radius
var double DIS2CENT = 0.29072
var double HALFSQRT2 = 0.7071
var double TORQUE2CURRENT = 1.0/0.0302; # [Amp/Nm]
var double CURRENT2VOLT = 1.0/1.0
var double GEARRATIO = 8.0/343.0
var double TWOPI = 2*3.141592
var double ENCODERCOUNTS = 500.0*4.0
var double ENC2SI = TWOPI*GEARRATIO/ENCODERCOUNTS




### We start by reading the encoders and sending the position information to ROS: ###


### READ WHEEL ENCODERS ###
loadComponent("ReadWheelEncoders","SOEM::ReadEncoders")
addPeer("Supervisor","ReadWheelEncoders")
ReadWheelEncoders.encoderbits = 65536
ReadWheelEncoders.enc2SI = array (ENC2SI, ENC2SI, ENC2SI, ENC2SI)
ReadWheelEncoders.configure
setActivity("ReadWheelEncoders",Ts,HighestPriority,ORO_SCHED_RT)
connect ("Soem.Slave_1002.Slave_1002_value","ReadWheelEncoders.enc1_in", ConnPolicy() ); #enc1[-]
connect ("Soem.Slave_1003.Slave_1003_value","ReadWheelEncoders.enc2_in", ConnPolicy() ); #enc2[-]
connect ("Soem.Slave_1004.Slave_1004_value","ReadWheelEncoders.enc3_in", ConnPolicy() ); #enc3[-]
connect ("Soem.Slave_1005.Slave_1005_value","ReadWheelEncoders.enc4_in", ConnPolicy() ); #enc4[-]


### DECOUPLE POSITION ###
loadComponent("BaseDecouplePosition","MatrixTransform")
addPeer("Supervisor","BaseDecouplePosition")
BaseDecouplePosition.Nrows = 3
BaseDecouplePosition.Ncolumns = 4
BaseDecouplePosition.configure
setActivity("BaseDecouplePosition",0.0,HighestPriority,ORO_SCHED_RT)
BaseDecouplePosition.function1 = array (-0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2)
BaseDecouplePosition.function2 = array ( 0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2, -0.25*WHEELRAD/HALFSQRT2,  0.25*WHEELRAD/HALFSQRT2)
BaseDecouplePosition.function3 = array ( 0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT,   0.25*WHEELRAD/DIS2CENT)
connect ("ReadWheelEncoders.out","BaseDecouplePosition.in", ConnPolicy() )


#### LOAD COMPONENT TO PUBLISH ODOMETRY ###
#loadComponent("Odometry","AMIGO::PublishOdometry")
loadComponent("Odometry","MSG::PublishOdometry")
addPeer("Supervisor","Odometry")
Odometry.base_link_frame = "/amigo/base_link"
Odometry.odom_frame = "/amigo/odom"
Odometry.configure
setActivity("Odometry",0.05,LowestPriority,ORO_SCHED_OTHER)
connect ("BaseDecouplePosition.out", "Odometry.pos", ConnPolicy() );
stream("Odometry.odom", ros.topic("/amigo/base/measurements"));




### Now we read the reference velocities from ROS and close the control loop:


### READ REFERENCE VELOCITIES ###
loadComponent("BaseReadReferenceVelocities","MSG::ReadTwistMsg")
addPeer("Supervisor","BaseReadReferenceVelocities")
#loadService("BaseReadReferenceVelocities","rosparam")
#BaseReadReferenceVelocities.rosparam.refreshProperties()
BaseReadReferenceVelocities.max_start_vel = 0.7
BaseReadReferenceVelocities.max_acc = array (0.7, 0.7, 2.0)
BaseReadReferenceVelocities.max_interval = 1.0
BaseReadReferenceVelocities.configure
setActivity("BaseReadReferenceVelocities",0.01,LowestPriority,ORO_SCHED_OTHER)
stream ("BaseReadReferenceVelocities.cmd_vel", ros.topic("/amigo/base/references"));

### DECOUPLE REFERENCE ###
loadComponent("BaseDecoupleVelReference","MatrixTransform")
addPeer("Supervisor","BaseDecoupleVelReference")
BaseDecoupleVelReference.Nrows = 4
BaseDecoupleVelReference.Ncolumns = 3
BaseDecoupleVelReference.configure
setActivity("BaseDecoupleVelReference",0.0,LowestPriority,ORO_SCHED_OTHER)
BaseDecoupleVelReference.function1 = array ( -HALFSQRT2/WHEELRAD,  HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function2 = array ( -HALFSQRT2/WHEELRAD, -HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function3 = array (  HALFSQRT2/WHEELRAD, -HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
BaseDecoupleVelReference.function4 = array (  HALFSQRT2/WHEELRAD,  HALFSQRT2/WHEELRAD,  DIS2CENT/WHEELRAD )
connect ("BaseReadReferenceVelocities.out","BaseDecoupleVelReference.in", ConnPolicy() )


### VELOCITY TO POSITION ###
loadComponent("BaseWheelVel2WheelPos","MATH::Integrator")
addPeer("Supervisor","BaseWheelVel2WheelPos")
BaseWheelVel2WheelPos.vector_size = 4
BaseWheelVel2WheelPos.configure
setActivity("BaseWheelVel2WheelPos",0.0,LowestPriority,ORO_SCHED_OTHER)
connect ("BaseDecoupleVelReference.out","BaseWheelVel2WheelPos.in", ConnPolicy() )
connect ("ReadWheelEncoders.out","BaseWheelVel2WheelPos.initial", ConnPolicy() ); # Use current position as initial position


### CALCULATE ERRORS ###
loadComponent("BaseCalculateErrors","MATH::Subtraction")
addPeer("Supervisor","BaseCalculateErrors")
BaseCalculateErrors.vectorsize = 4;
BaseCalculateErrors.configure
setActivity("BaseCalculateErrors",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseWheelVel2WheelPos.out","BaseCalculateErrors.in_plus", ConnPolicy() );
connect ("ReadWheelEncoders.out","BaseCalculateErrors.in_minus", ConnPolicy() ); #[m \n m \n rad]


### GAIN ###
loadComponent("BaseControllerGains","MATH::Gains")
addPeer("Supervisor","BaseControllerGains")
BaseControllerGains.vectorsize = 4;
#BaseControllerGains.gain = array (0.4, 0.4, 0.4, 0.4) # 10-08-12
BaseControllerGains.gain = array (5.0, 5.0, 5.0, 5.0)
BaseControllerGains.configure
setActivity("BaseControllerGains",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseCalculateErrors.out","BaseControllerGains.in", ConnPolicy() )


### INTEGRATOR ###
loadComponent("BaseIntegrator","FILTERS::WeakIntegrator")
addPeer("Supervisor","BaseIntegrator")
BaseIntegrator.vector_size = 4;
BaseIntegrator.sampling_time = Ts
BaseIntegrator.zero_frequency = array (0.0001, 0.0001, 0.0001, 0.0001)
BaseIntegrator.configure
setActivity("BaseIntegrator",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseControllerGains.out","BaseIntegrator.in", ConnPolicy() )


### LEADLAG ###
loadComponent("BaseLeadLag","FILTERS::LeadLags")
addPeer("Supervisor","BaseLeadLag")
BaseLeadLag.vector_size = 4;
BaseLeadLag.sampling_time = Ts
BaseLeadLag.zero_frequency = array (15.0, 15.0, 15.0, 15.0)
BaseLeadLag.pole_frequency = array (50.0, 50.0, 50.0, 50.0)
BaseLeadLag.configure
setActivity("BaseLeadLag",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseIntegrator.out","BaseLeadLag.in", ConnPolicy() )


### LOWPASS ###
loadComponent("BaseSecondOrderLowPass","FILTERS::SecondOrderLowPasses")
addPeer("Supervisor","BaseSecondOrderLowPass")
BaseSecondOrderLowPass.vector_size = 4;
BaseSecondOrderLowPass.sampling_time = Ts
BaseSecondOrderLowPass.pole_frequency = array (100.0, 100.0, 100.0, 100.0)
BaseSecondOrderLowPass.pole_damping = array (0.7, 0.7, 0.7, 0.7)
BaseSecondOrderLowPass.configure
setActivity("BaseSecondOrderLowPass",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseLeadLag.out","BaseSecondOrderLowPass.in", ConnPolicy() )


### SI2VOLT ###
loadComponent("BaseSI2Volt","MATH::Gains")
addPeer("Supervisor","BaseSI2Volt")
BaseSI2Volt.vectorsize = 4;
var double SI2V = TORQUE2CURRENT * CURRENT2VOLT * GEARRATIO
BaseSI2Volt.gain = array (SI2V, SI2V, SI2V, SI2V)
BaseSI2Volt.configure
setActivity("BaseSI2Volt",0.0,HighestPriority,ORO_SCHED_RT)
connect ("BaseSecondOrderLowPass.out","BaseSI2Volt.in", ConnPolicy() )
connect ("BaseSI2Volt.out", "AnalogOuts.wheels", ConnPolicy() ); # Send to Soem again for output



### Now the whole control loop is closed. Lets add some aditional components for fun and safety:


#### LOAD COMPONENT FOR SAFETY ###
loadComponent("BaseSafety","AMIGO::BaseSafety")
addPeer("Supervisor","BaseSafety")
BaseSafety.max_velocities = array (1.8, 1.0, 3.1415)
BaseSafety.max_errors = array ( 2.2, 2.2, 2.2, 2.2 )
BaseSafety.max_voltage = 9.9
BaseSafety.configure
setActivity("BaseSafety",0.0,HighestPriority/2,ORO_SCHED_RT)
connect ("BaseReadReferenceVelocities.out", "BaseSafety.ref", ConnPolicy() )
connect ("BaseCalculateErrors.out", "BaseSafety.error", ConnPolicy() )
connect ("BaseSI2Volt.out", "BaseSafety.voltage", ConnPolicy() )
#connect ("BaseSafety.wheel_amplifiers", "DigitalOuts.in1", ConnPolicy() ); # for reading back the ebutton info
connect ("BaseSafety.wheel_amplifiers", "DigitalOuts2.in2", ConnPolicy() ); # amplifiers

#### LOAD COMPONENT TO PUBLISH DIAGNOSTICS ###
loadComponent("BaseDiagnostics","ROS::RosDiagnostics")
addPeer("Supervisor","BaseDiagnostics")
BaseDiagnostics.statusname = "BaseController"
BaseDiagnostics.NumberOfVectorPorts = 3
BaseDiagnostics.NumberOfBoolPorts = 1
BaseDiagnostics.configure
setActivity("BaseDiagnostics",1.0,LowestPriority,ORO_SCHED_OTHER)
BaseDiagnostics.vecname1 = "Reference"
BaseDiagnostics.vecname2 = "Error"
BaseDiagnostics.vecname3 = "Voltage"
BaseDiagnostics.boolname1 = "Amplifiers"
connect ("BaseReadReferenceVelocities.out", "BaseDiagnostics.vec1", ConnPolicy() )
connect ("BaseCalculateErrors.out", "BaseDiagnostics.vec2", ConnPolicy() )
connect ("BaseSI2Volt.out", "BaseDiagnostics.vec3", ConnPolicy() )
connect ("BaseSafety.wheel_amplifiers", "BaseDiagnostics.bool1", ConnPolicy() )
stream("BaseDiagnostics.diagnostics", ros.topic("/diagnostics"));
stream("BaseDiagnostics.diagnostics", ros.topic("/base_diagnostics"));


#### LOAD COMPONENT TO PUBLISH STATUS ###
loadComponent("BaseStatus","ROS::BoolToROS")
addPeer("Supervisor","BaseStatus")
BaseStatus.NumberOfBoolPorts = 1
BaseStatus.configure
setActivity("BaseStatus",0.5,LowestPriority,ORO_SCHED_OTHER)
connect ("BaseSafety.wheel_amplifiers", "BaseStatus.bool_in1", ConnPolicy() )
stream("BaseStatus.bool_out1", ros.topic("/base_status"));


### We start by reading the encoders and sending the position information to ROS: ###
#### START COMPONENTS ####
Supervisor.AddAllwaysOnPeer ("ReadWheelEncoders")
Supervisor.AddAllwaysOnPeer ("BaseDecouplePosition")
Supervisor.AddAllwaysOnPeer ("Odometry")


### Now we read the reference velocities from ROS and close the control loop:
#### THESE COMPONENTS CAN BE STOPPED BY THE SUPERVISOR ####
Supervisor.NameBodyPart( 1, "base")
stream( "Supervisor.base_fireup", ros.topic("/base_fireup") )
stream( "Supervisor.base_enabled", ros.topic("/base_enabled"));
Supervisor.AddPeerToBodyPart ("BaseReadReferenceVelocities",1 )
Supervisor.AddPeerToBodyPart ("BaseDecoupleVelReference",1 )
Supervisor.AddPeerToBodyPart ("BaseWheelVel2WheelPos",1 )
Supervisor.AddPeerToBodyPart ("BaseCalculateErrors",1 )
Supervisor.AddPeerToBodyPart ("BaseControllerGains",1 )
Supervisor.AddPeerToBodyPart ("BaseLeadLag",1 )
Supervisor.AddPeerToBodyPart ("BaseIntegrator",1 )
Supervisor.AddPeerToBodyPart ("BaseSecondOrderLowPass",1 )
Supervisor.AddPeerToBodyPart ("BaseSI2Volt",1 )

### Now the whole control loop is closed. Lets add some aditional components for fun and safety:
Supervisor.AddAllwaysOnPeer ("BaseSafety" )
Supervisor.AddAllwaysOnPeer ("BaseDiagnostics")
Supervisor.AddAllwaysOnPeer ("BaseStatus")
#Supervisor.AddAllwaysOnPeer ("BaseSupervisor") # To be removed?

